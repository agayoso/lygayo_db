
* Programa de Aplicacion del Proxy Mean Test para la Identificacion de Pobreza Monetaria
* Ministerio de Desarrollo Social de Paraguay
* Version: Diciembre 2024
* Autora: Lyliana Gayoso de Ervin
*===================================================================================================================
	clear all
	set more off
	pause on

/*=======================================================
                   00: Directory setup                                             
=========================================================*/
	global dir      "C:\Users\lylig\Documents\Consultorias\2024\Unicef\Archivos Finales"
	global results  "$dir\results"
	global data     "$dir\data" 
	
	use "$results\IPM_PMT_base_2022_cleaned_vars.dta", clear
	
	*se restringe la base al dominio urbano
	keep if dominio==1

/*=======================================================
           01: CONSTRUCCION DE INDICADORES DEL PMT
=========================================================*/
//creacion variables categoricas VIVIENDA
	gen cat_agua_proveedor_beber=(tipo_agua_proveedor_beber==1 | tipo_agua_proveedor_beber==5 | tipo_agua_proveedor_beber==11) if tipo_agua_proveedor_beber!=.
	gen cat_banho_desague=(inrange(tipo_banho_desague,1,2)) if tipo_banho_desague!=.
	gen cat_basura=(inrange(tipo_basura,2,3)) if tipo_basura!=.
	gen cat_combustible=(tipo_combustible==2 | tipo_combustible==7) if tipo_combustible!=.
	gen cat_hogar_unipersonal=(tipo_hogar==1) if tipo_hogar!=.


//creacion variables categoricas JEFE
	gen jefe_cat_empleo= (inrange(jefe_tipo_empleo,1,3)) if jefe_tipo_empleo!=. 
	gen jefe_cat_idioma=(inrange(jefe_idioma,3,4)) if jefe_idioma!=.


//creacion variables categoricas HOGAR
	gen hh_lndormitorios_pc=ln(nro_dormitorios/totpers)
	gen hh_lntotpers=ln(totpers)
	gen hh_porc_0a14 = (nro_5menos + nro_6a14)/totpers
	gen hh_porc_trabajan=nro_trabajo/totpers
	gen hh_porc_seguro=nro_tiene_seguro/totpers

/*=======================================================
              02: IMPLEMENTACION DEL PMT                                              
=========================================================*/
//Primero se definen las ponderaciones del PMT urbano
gen w_agua_proveedor_beber		= 0.1007666
gen w_banho_desague				= 0.0521674
gen w_basura					= 0.0985621
gen w_combustible				= 0.0961469
gen w_hogar_unipersonal			= 0.1059598
gen w_lndormitorios_pc			= 0.1966894
gen w_lntotpers					= -0.2057523
gen w_porc_0a14					= -0.4550504
gen w_porc_seguro				= 0.3278719
gen w_porc_trabajan				= 0.5247115
gen w_jefe_anios_estudio		= 0.0156513
gen w_jefe_cat_empleo	    	= 0.1498015
gen w_jefe_cat_idioma			= 0.0797395
gen w_tiene_acondicionador_aire	= 0.1082751
gen w_tiene_auto_camion			= 0.1714332
gen w_cocina_elec				= 0.0424315
gen w_computadora				= 0.1464662
gen w_horno_microondas			= 0.0906720
gen w_internet					= 0.0718241
gen w_televisor					= 0.0933939
gen w_termocalefon				= 0.2819824
gen w_tiene_tv_cable			= 0.0570942
gen constante       			= 13.3317300

//Luego, se define la linea de pobreza total y extrema estimada 
//para el modelo urbano
gen lpobtot_urbano = 13.62419
gen lpobext_urbano = 13.02462

//Ahora, se calcula el ingreso estimado del hogar 
gen lninc_est = constante + cat_agua_proveedor_beber*w_agua_proveedor_beber + cat_banho_desague*w_banho_desague + ///
				cat_basura*w_basura + cat_combustible*w_combustible + cat_hogar_unipersonal*w_hogar_unipersonal + ///
				hh_lndormitorios_pc*w_lndormitorios_pc + hh_lntotpers*w_lntotpers + hh_porc_0a14*w_porc_0a14 + ///
				hh_porc_seguro*w_porc_seguro + hh_porc_trabajan*w_porc_trabajan + jefe_anios_estudio*w_jefe_anios_estudio + ///
				jefe_cat_empleo*w_jefe_cat_empleo + jefe_cat_idioma*w_jefe_cat_idioma + ///
				tiene_acondicionador_aire*w_tiene_acondicionador_aire + tiene_auto_camion*w_tiene_auto_camion + ///
				tiene_cocina_elec*w_cocina_elec + tiene_computadora*w_computadora + tiene_horno_microondas*w_horno_microondas + ///
				tiene_internet*w_internet + tiene_televisor*w_televisor + tiene_termocalefon*w_termocalefon + ///
				tiene_tv_cable*w_tiene_tv_cable

				
//Luego, identificamos el estatus de pobreza del hogar
gen     status_pobreza=1 if lninc_est<=lpobext_urbano
replace status_pobreza=2 if lninc_est>lpobext_urbano & lninc_est<=lpobtot_urbano
replace status_pobreza=3 if lninc_est>lpobtot_urbano 

label define st 1 "pobre extremo" 2 "pobre no extremo" 3 "no pobre monetario" 
label value status_pobreza st 

tab pobrezai [w=round(facpob)]
tab status_pobreza [w=round(facpob)]

keep hhid upm nvivi nhoga dpto area dominio status_pobreza 

save "$results\pmt_urbano.dta", replace
